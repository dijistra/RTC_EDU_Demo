plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion project.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField('String', 'TERMS_OF_SERVICE_URL', "\"$TERMS_OF_SERVICE_URL\"")
        buildConfigField('String', 'PRIVACY_POLICY_URL', "\"$PRIVACY_POLICY_URL\"")
        buildConfigField('String', 'LOGIN_URL', "\"${project.LOGIN_URL}\"")

        ensureProperty('APP_ID')
        buildConfigField('String', 'APP_ID', "\"${project.APP_ID}\"")
        ensureProperty('APP_KEY')
        buildConfigField('String', 'APP_KEY', "\"${project.APP_KEY}\"")
        ensureProperty('VOLC_AK')
        buildConfigField('String', 'VOLC_AK', "\"${project.VOLC_AK}\"")
        ensureProperty('VOLC_SK')
        buildConfigField('String', 'VOLC_SK', "\"${project.VOLC_SK}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
}

repositories {
    maven {
        url "https://maven.byted.org/nexus/content/repositories/ss_app_android/"
    }
    flatDir {
        dirs 'libs', project(':scene-core').files('libs')
    }
}
dependencies {
    implementation "androidx.appcompat:appcompat:$AppCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$RecyclerViewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$ConstraintLayoutVersion"
    implementation "com.google.android.material:material:$MaterialVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation project(":scene-core")

    implementation "org.greenrobot:eventbus:$EventBusVersion"
    implementation "com.google.code.gson:gson:$GsonVersion"
}

private def ensureProperty(String propertyName) {
    if (!project.hasProperty(propertyName) || project[propertyName].isEmpty()) {
        throw new GradleException("NO ${propertyName} defined in gradle.properties!")
    }
}